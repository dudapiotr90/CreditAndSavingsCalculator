plugins {
    id 'java'
    id 'jacoco'
}

group = 'pl.zajavka'
version = '0.0.1'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

ext {
    logbackVersion = '1.4.5'
    junitVersion = '5.9.2'
    mockitoVersion = '4.8.1'
    springVersion = '5.3.20'
    lombokVersion = '1.18.26'
    plantUMLVersion = '2.0.0'
}

dependencies {
    implementation "de.elnarion.util:plantuml-generator-util:$plantUMLVersion"

    implementation "ch.qos.logback:logback-classic:$logbackVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    implementation "org.springframework:spring-core:$springVersion"
    implementation "org.springframework:spring-beans:$springVersion"
    implementation "org.springframework:spring-context:$springVersion"
    implementation "org.springframework:spring-context-support:$springVersion"
    implementation "org.springframework:spring-expression:$springVersion"

    testImplementation "ch.qos.logback:logback-classic:$logbackVersion"

    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"

    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"

    testImplementation "org.springframework:spring-test:$springVersion"

}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}
jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtmlCustom')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.5
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

compileJava.options.encoding = 'UTF-8'